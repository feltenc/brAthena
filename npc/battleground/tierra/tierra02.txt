/*.-----------------------------------------------------------------------------.
  .          ____                          __                                   .
  .         /\  _`\                       /\ \__  __                            .
  .         \ \ \/\_\  _ __    __     __  \ \ ,_\/\_\  __  __     __            .
  .          \ \ \/_/_/\`'__\/'__`\ /'__`\ \ \ \/\/\ \/\ \/\ \  /'__`\          .
  .           \ \ \s\ \ \ \//\  __//\ \d\.\_\ \ \_\ \ \ \ \_/ |/\  __/          .
  .            \ \____/\ \_\\ \____\ \__/.\_\\ \__\\ \_\ \___/ \ \____\         .
  .             \/___/  \/_/ \/____/\/__/\/_/ \/__/ \/_/\/__/   \/____/         .
  .                                                                             .
  .                  2014~2016 © Creative Services and Developent               .
  .                            www.creativesd.com.br                            .
  .                          www.sbkmod.ragnagreen.com                          .
  .-----------------------------------------------------------------------------.
  . Script:                                                                     .
  .    2° Desfiladeiro de Tierra                                                .
  .-----------------------------------------------------------------------------.
  . Autor: Romulo SM (sbk_) (HerculesWS)                           Versão: 6.0  .
  .                                                                             .
  . Compátivel:	eAthena e derivações.                    	                    .
  .-----------------------------------------------------------------------------.
  . Descrição:                                                                  .
  .    Batalha no qual os jogadores devem destruir o suprimento do adversário.  .
  *------------------------------------------------------------------------------*/
  
// Engine das Batalhas Campais do Desfiladeiro de Tierra
// -------------------------------------------------------
bat_a02,15,15,3	script	start#bat_a02	CLEAR_NPC,{
OnInit:
	mapwarp "bat_a02","bat_room",154,150;
	queue_create(2, "2º Desfiladeiro de Tierra", "start#bat_a02::OnReadyCheck");
	set $@TierraBG2_id1, bg_create_team("bat_a02", 50, 374, 352, 342, 0, "start#bat_a02::OnGuillaumeQuit", "", "start#bat_a02::OnGuillWO");
	set $@TierraBG2_id2, bg_create_team("bat_a02", 42, 16, 353, 52, 1, "start#bat_a02::OnCroixQuit", "", "start#bat_a02::OnCroixWO");
	set .global_announce, 0;
	set .BlockStatus, 0;
	donpcevent "start#bat_a02::OnReadyCheck";
	end;

OnReadyCheck:
	if( .BlockStatus )
	{
		sleep 5000;
		donpcevent "start#bat_a02::OnReadyCheck";
	}
		
	set .BlockStatus, 1;

	set .@min_player, callfunc("bg_getarenainfo", 2, "min_player");
	set .@max_player, callfunc("bg_getarenainfo", 2, "max_player");
	set .@a_name$, callfunc("bg_getarenainfo", 2, "arena_name");
	set .@min_player, (.@min_player*2);
	
	if( .@max_player > MAX_BG_MEMBERS )
	{
		bg_console "(BG_ID: 2) Maximo de jogadores em uma equipe ultrapassa o limite estabelecido, definindo o maximo de jogadores para " + MAX_BG_MEMBERS + ".";
		set .@max_player, MAX_BG_MEMBERS;
	}
	
	queue_get_players 2;
	set .@playlist_team1, bg_team_size($@TierraBG2_id1);
	set .@playlist_team2, bg_team_size($@TierraBG2_id2);
	if( !.@queue_count )
	{
		set .BlockStatus, 0;
		end;
	}
	else if( $@TierraBG2 && $@TierraBG2 < 2 && .@queue_count && (.@playlist_team1 < .@max_player || .@playlist_team2 < .@max_player) )
	{
		set .@guill, 0;
		set .@croix, 0;
		set .@count_g, 0;
		set .@count_c, 0;
		
		// ---------------------------------------------------
		// -             Igualando os Exércitos              -
		// ---------------------------------------------------
		if( .@playlist_team1 < .@playlist_team2 )
		{
			set .@guill, .@playlist_team2-.@playlist_team1;
			for( set .@i,0; .@i < .@guill; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG2_id1, 2, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_g, .@count_g + 1;
				message .@queue_player$[.@i], "Você ingressou no Exército de Guillaume em uma batalha em " + .@a_name$ + " que já está acontecendo, e será teleportado em 10 segundos.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 2; // Reset
			set .@playlist_team1, bg_team_size($@TierraBG2_id1);
		}
		else if( .@playlist_team2 < .@playlist_team1 )
		{
			set .@croix, .@playlist_team1-.@playlist_team2;
			for( set .@i,0; .@i < .@croix; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG2_id1, 2, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_c, .@count_c + 1;
				message .@queue_player$[.@i], "Você ingressou no Exército de Croix em uma batalha em " + .@a_name$ + " que já está acontecendo, e será teleportado em 10 segundos.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 2; // Reset
			set .@playlist_team2, bg_team_size($@TierraBG2_id2);
		}
		// ---------------------------------------------------
		// -            Expandindo os Exércitos              -
		// ---------------------------------------------------
		if( .@queue_count && .@playlist_team1 == .@playlist_team2 && .@playlist_team1 < .@max_player && .@playlist_team2 < .@max_player )
		{
			set .@guill, .@max_player-.@playlist_team1;
			set .@croix, .@max_player-.@playlist_team2;
			
			if( .@queue_count > (.@guill+.@croix) )
				set .@queue_count, (.@guill+.@croix);
				
			if( .@queue_count%2 )
				set .@queue_count, .@queue_count-1;
			
			set .@size, .@queue_count/2;
			for( set .@i, 0; .@i < .@size; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG2_id1, 2, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_g$[.@count_g], .@queue_player$[.@i];
				set .@count_g, .@count_g + 1;
			}
			
			for( set .@i,.@size; .@i < .@queue_count; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG2_id2, 2, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_c$[.@count_c], .@queue_player$[.@i];
				set .@count_c, .@count_c + 1;
			}
		}
		
		if( .@count_c || .@count_g )
		{
			announce "[Batalhas Campais]: " + (.@count_c+.@count_g) + " jogadores estão sendo enviados para uma batalha que está acontecendo em " + .@a_name$ + ", os Exércitos foram equilibrados e expandidos.", bc_all, "0xFFCE00";
			sleep 3000;
		}
		
		deletearray .@queue_cid;
		deletearray .@queue_aid;
		deletearray .@queue_player$;
		set .@queue_count, 0;
		queue_get_players 2; // Reset
		
		set .@queue_size, queue_size(2);
		set .@team1_size, bg_team_size($@TierraBG2_id1);
		set .@team2_size, bg_team_size($@TierraBG2_id2);
		
		if( .@team1_size < .@max_player || .@team2_size < .@max_player )
		{
			set .@size, (2*.@max_player)-(.@team1_size+.@team2_size)-.@queue_size;
			announce "[Batalhas Campais]: Ainda há " + (.@size) + " vagas para uma batalha que está acontecendo em " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	else if( $@TierraBG2 && !.global_announce && .@queue_count >= .@min_player )
	{
		announce "[Batalhas Campais]: A batalha está completa, mas há uma batalha acontecendo em " + .@a_name$ + ", todos jogadores que estão na fila de espera devem aguardar.", bc_all, "0xFFCE00";
		set .global_announce, 1;
	}
	else if( .@queue_count < .@min_player )
		announce "[Batalhas Campais]: Falta " + (.@min_player-.@queue_count) + " jogadores na batalha em " + .@a_name$ + "!", bc_all, "0xFFCE00";
	else
	{
		// ---------------------------------------------------
		// -                  Balanceamento                  -
		// ---------------------------------------------------
		if( .@queue_count > (.@max_player*2) )
			set .@queue_count, (.@max_player*2);
			
		if( .@queue_count%2 )
			set .@queue_count, .@queue_count-1;
			
		if( .@queue_count >= .@min_player )
		{
			for( set .@i,0; .@i < .@queue_count; set .@i, .@i + 2 )
			{
				// Ingressando no Exército de Guillaume
				bg_join_team $@TierraBG2_id1, 2, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				message .@queue_player$[.@i], "Você ingressou no Exército de Guillaume, será teleportado em 10 segundos.";
				
				// Ingressando no Exército de Croix
				bg_join_team $@TierraBG2_id2, 2, .@queue_aid[.@i+1];
				queue_leave 0, .@queue_aid[.@i+1];
				message .@queue_player$[.@i+1], "Você ingressou no Exército de Croix, será teleportado em 10 segundos.";
			}
		}

		bg_refresh_patent $@TierraBG2_id1;
		bg_refresh_patent $@TierraBG2_id2;
		set .global_announce, 0;
		set $@TierraBG2, 1;
		announce "[Batalhas Campais]: Ambos os Exércitos, estão completo boa sorte nas batalhas em " + .@a_name$ + ".", bc_all, "0xFFCE00";
		set $@TierraBG2_CroixScore, 0;
		set $@TierraBG2_GuillScore, 0;
		bg_updatescore "bat_a02",0,0;
		donpcevent "start#bat_a02::OnEnable";
		sleep 3000;
		initnpctimer;
		
		if( $@BG_MODE && callfunc("bg_checkchangearena") == true )
			donpcevent "BattleGround#queue_main::OnReload";
			
		if( .@queue_count < (2*.@max_player) )
		{
			sleep 3000;
			set .@count, (2*.@max_player)-.@queue_count;
			announce "[Batalhas Campais]: Ainda há " + .@queue_count + " vagas nas batalhas em " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	set .BlockStatus, 0;
	end;
	
OnEnable:
	bg_set_respawn $@TierraBG2_id1, 352, 342;
	bg_set_respawn $@TierraBG2_id2, 353, 52;
	donpcevent "OBJ#bat_a02_a::OnKill";
	donpcevent "OBJ#bat_a02_a::OnEnable";
	donpcevent "OBJ#bat_a02_b::OnKill";
	donpcevent "OBJ#bat_a02_b::OnEnable";
	donpcevent "barricade#bat_a02_a::OnKill";
	donpcevent "barricade#bat_a02_a::OnEnable";
	donpcevent "barricade#bat_a02_b::OnKill";
	donpcevent "barricade#bat_a02_b::OnEnable";
	donpcevent "OBJ#bat_a02_n::OnKill";
	donpcevent "OBJ#bat_a02_n::OnEnable";
	donpcevent "NOBJ_mob#bat_a02_a::OnKill";
	donpcevent "NOBJ_mob#bat_a02_b::OnKill";
	donpcevent "Terapeuta da Batalha#a02_a::OnEnable";
	donpcevent "Terapeuta da Batalha#a02_b::OnEnable";
	donpcevent "countdown#bat_a02::OnEnable";
	donpcevent "time_event#bat_a02::OnEnable";
	disablenpc "Ferreiro de Guillaume#a02";
	disablenpc "Ferreiro de Croix#bat_a02";
	disablenpc "Tenente de Guillaume#a02_a";
	disablenpc "Tenente de Croix#a02_b";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	set .@char_id, getcharid(0);
	callfunc "bg_ranking", .@char_id, 2, 0, 1; // BG Queue Ranking: Computer negative vote.
	donpcevent "start#bat_a02::OnReadyCheck";
	end;

OnGuillWO:
	if( queue_size(2) )
		end;
		
	set $@TierraBG2, 2;
	set $@TierraBG2_Victory, 2;
	donpcevent "Terapeuta da Batalha#a02_a::OnStop";
	donpcevent "Terapeuta da Batalha#a02_b::OnStop";
	donpcevent "countdown#bat_a02::OnStop";
	donpcevent "time_event#bat_a02::OnStop";
	donpcevent "start#bat_a02_Out::OnBegin";
	mapannounce "bat_a02", "Tenente de Croix Swandery: Os covardes de Guillaume fugiram. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
	enablenpc "Tenente de Guillaume#a02_a";
	enablenpc "Tenente de Croix#a02_b";
	sleep 2000;
	bg_warp $@TierraBG2_id1,"bat_a02",50,374;
	bg_warp $@TierraBG2_id2,"bat_a02",42,16;
	end;
	
OnCroixWO:
	if( queue_size(2) )
		end;
		
	set $@TierraBG2, 2;
	set $@TierraBG2_Victory, 1;
	donpcevent "Terapeuta da Batalha#a02_a::OnStop";
	donpcevent "Terapeuta da Batalha#a02_b::OnStop";
	donpcevent "countdown#bat_a02::OnStop";
	donpcevent "time_event#bat_a02::OnStop";
	donpcevent "start#bat_a02_Out::OnBegin";
	mapannounce "bat_a02", "Tenente de Guillaume Axl Rose: Os covardes de Croix fugiram. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
	enablenpc "Tenente de Guillaume#a02_a";
	enablenpc "Tenente de Croix#a02_b";
	sleep 2000;
	bg_warp $@TierraBG2_id1,"bat_a02",50,374;
	bg_warp $@TierraBG2_id2,"bat_a02",42,16;
	end;

// Checagem de Jogadores na Arena.
// -------------------------------------------------------
OnTimer10000:
	stopnpctimer;
	if( !getmapusers("bat_a02") )
	{
		set $@TierraBG2, 0;
		bg_kick_team $@TierraBG2_id1;
		bg_kick_team $@TierraBG2_id2;
		donpcevent "start#bat_a02_Out::OnStop";
		
		if( !$@BG_MODE || $BG_ARENA == 2 )
			donpcevent "start#bat_a02::OnReadyCheck";
	}
	else
		initnpctimer;
	end;
	end;
}

bat_a02,15,16,3	script	OBJ#bat_a02_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@TierraBG2_id1,"bat_a02",176,345,"Suprimentos",1909,"","OBJ#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_a::OnMyMobDead") < 1) {
		set $@TierraBG2_CroixScore, $@TierraBG2_CroixScore+1;
		set .@score_round, callfunc("bg_getarenainfo",2,"score_round");
		bg_updatescore "bat_a02", $@TierraBG2_GuillScore, $@TierraBG2_CroixScore;
		
		if( $@TierraBG2_CroixScore >= .@score_round )
		{
			donpcevent "Terapeuta da Batalha#a02_a::OnStop";
			donpcevent "Terapeuta da Batalha#a02_b::OnStop";
			donpcevent "countdown#bat_a02::OnStop";
			donpcevent "time_event#bat_a02::OnStop";
			set $@TierraBG2, 2;
			set $@TierraBG2_Victory, 2;
			enablenpc "Tenente de Guillaume#a02_a";
			enablenpc "Tenente de Croix#a02_b";
			donpcevent "start#bat_a02_Out::OnBegin";
			mapannounce "bat_a02", "Tenente de Croix Swandery: Uau! Nós destruímos os Suprimentos do Exército de Guillaume. Nós vencemos!",bc_map,"0xFFCE00";
			bg_warp $@TierraBG2_id1,"bat_a02",50,374;
			bg_warp $@TierraBG2_id2,"bat_a02",42,16;
		}
		else {
			donpcevent "start#bat_a02::OnEnable";
			bg_warp $@TierraBG2_id1,"bat_a02",352,342;
			bg_warp $@TierraBG2_id2,"bat_a02",353,52;
			sleep 1000;
			mapannounce "bat_a02", "Tenente de Croix Swandery: Uau! Nós destruímos os Suprimentos do Exército de Guillaume. Nós vencemos esta rodada!",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a02,15,17,3	script	OBJ#bat_a02_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@TierraBG2_id2,"bat_a02",167,50,"Suprimentos",1910,"","OBJ#bat_a02_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_b::OnMyMobDead") < 1) {
		set $@TierraBG2_GuillScore, $@TierraBG2_GuillScore+1;
		set .@score_round, callfunc("bg_getarenainfo",2,"score_round");
		bg_updatescore "bat_a02", $@TierraBG2_GuillScore, $@TierraBG2_CroixScore;
		if( $@TierraBG2_GuillScore >= .@score_round )
		{
			donpcevent "Terapeuta da Batalha#a02_a::OnStop";
			donpcevent "Terapeuta da Batalha#a02_b::OnStop";
			donpcevent "countdown#bat_a02::OnStop";
			donpcevent "time_event#bat_a02::OnStop";
			set $@TierraBG2, 2;
			set $@TierraBG2_Victory, 1;
			enablenpc "Tenente de Guillaume#a02_a";
			enablenpc "Tenente de Croix#a02_b";
			donpcevent "start#bat_a02_Out::OnBegin";
			mapannounce "bat_a02", "Tenente de Guillaume Axl Rose : Uau! Nós destruímos os Suprimentos do Exército de Croix. Nós vencemos!",bc_map,"0xFFCE00";
			bg_warp $@TierraBG2_id1,"bat_a02",50,374;
			bg_warp $@TierraBG2_id2,"bat_a02",42,16;
		}
		else {
			donpcevent "start#bat_a02::OnEnable";
			bg_warp $@TierraBG2_id1,"bat_a02",352,342;
			bg_warp $@TierraBG2_id2,"bat_a02",353,52;
			sleep 1000;
			mapannounce "bat_a02", "Tenente de Croix Swandery: Uau! Nós destruímos os Suprimentos do Exército de Guillaume. Nós vencemos esta rodada!",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a02,15,18,3	script	barricade#bat_a02_a	CLEAR_NPC,{
OnEnable:
	for ( set .@i,185; .@i < 202; set .@i,.@i+1 )
		bg_monster $@TierraBG2_id1,"bat_a02",.@i,266,"Barricada",1906,"","barricade#bat_a02_a::OnMyMobDead";
	setwall "bat_a02",186,266,16,6,1,"bat_a02_c1";
	end;

OnKill:
	killmonster "bat_a02","barricade#bat_a02_a::OnMyMobDead";
	delwall "bat_a02_c1";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","barricade#bat_a02_a::OnMyMobDead") < 17) {
		killmonster "bat_a02","barricade#bat_a02_a::OnMyMobDead";
		delwall "bat_a02_c1";
		enablenpc "Ferreiro de Guillaume#a02";
		mapannounce "bat_a02", "Tenente de Guillaume Axl Rose : A Barrica do desfiladeiro foi destruída! Aonde está nosso Ferreiro? Precisamos reconstruir a barricada.",bc_map,"0xFFCE00";
	}
	end;
}

bat_a02,15,19,3	script	barricade#bat_a02_b	CLEAR_NPC,{
OnEnable:
	for ( set .@i,169; .@i < 186; set .@i,.@i+1 )
		bg_monster $@TierraBG2_id2,"bat_a02",.@i,129,"Barricade",1906,"","barricade#bat_a02_b::OnMyMobDead";
	setwall "bat_a02",170,129,16,6,1,"bat_a02_g1";
	end;

OnKill:
	killmonster "bat_a02","barricade#bat_a02_b::OnMyMobDead";
	delwall "bat_a02_g1";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","barricade#bat_a02_b::OnMyMobDead") < 17) {
		killmonster "bat_a02","barricade#bat_a02_b::OnMyMobDead";
		delwall "bat_a02_g1";
		enablenpc "Ferreiro de Croix#bat_a02";
		mapannounce "bat_a02", "Tenente de Croix Swandery : A Barricada do desfiladeiro foi destruída! Aonde está nosso Ferreiro? Precisamos reconstruir a barricada.",bc_map,"0xFFCE00";
	}
	end;
}

bat_a02,15,20,3	script	OBJ#bat_a02_n	CLEAR_NPC,{
OnEnable:
	monster "bat_a02",273,203,"Bandeira Neutra",1911,1,"OBJ#bat_a02_n::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_n::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_n::OnMyMobDead") < 17) {
		//bg_team_setxy getcharid(4),56,212;
		bg_set_respawn getcharid(4),301,209;
		if (getcharid(4) == $@TierraBG2_id1) {
			donpcevent "NOBJ_mob#bat_a02_a::OnEnable";
			mapannounce "bat_a02", "O Exército de Guillaume obteve um Ponto Estratégico, portanto está com vantagem.",bc_map,"0xFFCE00";
		}
		else {
			donpcevent "NOBJ_mob#bat_a02_b::OnEnable";
			mapannounce "bat_a02", "O Exército de Croix obteve um Ponto Estratégico, portanto está com vantagem.",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a02,15,21,3	script	NOBJ_mob#bat_a02_a	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a02_b::OnKill";
	bg_monster $@TierraBG2_id1,"bat_a02",272,204,"Guardião de Guillaume",1949,"Guardião","NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id1,"bat_a02",272,213,"Guardião de Guillaume",1949,"Guardião","NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id1,"bat_a02",273,197,"Guardião de Guillaume",1950,"Guardião","NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a02,15,22,3	script	NOBJ_mob#bat_a02_b	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a02_a::OnKill";
	bg_monster $@TierraBG2_id2,"bat_a02",272,204,"Guardião de Croix",1949,"Guardião","NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id2,"bat_a02",272,213,"Guardião de Croix",1949,"Guardião","NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id2,"bat_a02",273,197,"Guardião de Croix",1950,"Guardião","NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","NOBJ_mob#bat_a02_b::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a02,185,270,1	script	Ferreiro de Guillaume#a02	4_M_REPAIR,{
	if (getcharid(4) == $@TierraBG2_id1) {
		mes "[Ferreiro de Guillaume]";
		mes "Estamos em urgência! A Barricada foi destruída!";
		mes "Podemos recuperar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouro.^000000";
		mes "Temos tudo isso exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Deixar assim.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro de Guillaume]";
				mes "Você trouxe pedras suficientes! Vamos reparar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal, e trançando as pedras com os Nervos de Urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro de Guillaume]";
				mes "Uau! Está feito.";
				mes "Agora estamos mais aliviados.";
				delitem 7049,50; //Pedras
				donpcevent "barricade#bat_a02_a::OnEnable";
				close2;
				disablenpc "Ferreiro de Guillaume#a02";
				end;
			}
			else {
				mes "[Ferreiro de Guillaume]";
				mes "Você não tem Pedras suficientes!";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "^3131FFPrecisamos de 50 Pedras.^000000";
				mes "Estamos ocupados, então por favor, se apresse.";
				close;
			}
		case 2:
			mes "[Ferreiro de Guillaume]";
			mes "Os inimigos estão chegando! Vamos evacuar e partir daqui!";
			close;
		}
	}
	else {
		mes "[Ferreiro de Guillaume]";
		mes "Lá, o inimigo está chegando";
		close;
	}

OnInit:
	disablenpc "Ferreiro de Guillaume#a02";
	end;
}

bat_a02,170,121,5	script	Ferreiro de Croix#bat_a02	4_M_REPAIR,{
	if (getcharid(4) == $@TierraBG2_id2) {
		mes "[Ferreiro de Croix]";
		mes "Estamos em urgência! A Barricada foi destruída!";
		mes "Podemos recuperar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouro.^000000";
		mes "Temos tudo isso exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Deixar assim.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro de Croix]";
				mes "Você trouxe pedras suficientes! Vamos reparar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal, e trançando as pedras com os Nervos de Urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro de Croix]";
				mes "Uau! Está feito.";
				mes "Agora estamos mais aliviados.";
				delitem 7049,50; //Pedras
				donpcevent "barricade#bat_a02_b::OnEnable";
				close2;
				disablenpc "Ferreiro de Croix#bat_a02";
				end;
			}
			else {
				mes "[Ferreiro de Croix]";
				mes "Você não tem Pedras suficientes!";
				next;
				mes "[Ferreiro de Croix]";
				mes "^3131FFPrecisamos de 50 Pedras.^000000";
				mes "Estamos ocupados, então por favor, se apresse.";
				close;
			}
		case 2:
			mes "[Ferreiro de Croix]";
			mes "There are enemies coming! Let's evacuate from here!";
			close;
		}
	}
	else {
		mes "[Ferreiro de Croix]";
		mes "Os inimigos estão chegando! Vamos evacuar e partir daqui!";
		close;
	}

OnInit:
	disablenpc "Ferreiro de Croix#bat_a02";
	end;
}

bat_a02,53,377,3	script	Terapeuta da Batalha#a02_a	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta da Batalha]";
	mes "Apenas feche seus olhos";
	mes "e respire fundo.";
	mes "Você pode se libertar da dor.";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Terapeuta da Batalha#a02_a";
	end;

OnStop:
	disablenpc "Terapeuta da Batalha#a02_a";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;

OnTimer26500:
	donpcevent "Terapeuta da Batalha#a02_a::OnEnable";
	end;
}

bat_a02,45,19,3	script	Terapeuta da Batalha#a02_b	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta da Batalha]";
	mes "Apenas feche seus olhos";
	mes "e respire fundo.";
	mes "Você pode se libertar da dor.";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Terapeuta da Batalha#a02_b";
	end;

OnStop:
	disablenpc "Terapeuta da Batalha#a02_b";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;

OnTimer26500:
	donpcevent "Terapeuta da Batalha#a02_b::OnEnable";
	end;
}

bat_a02,60,216,3	script	Fantasma#bat_a02_n	4_GHOSTRING,{
	specialeffect2 EF_HEAL;
	mes "[Fantasma]";
	mes "Boo...Boo...";
	close;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;

OnTimer26500:
	donpcevent "Fantasma#bat_a02_n::OnEnable";
	end;
}

bat_a02,194,267,0	script	barri_warp_up#bat_a02_a	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id1)
		warp "bat_a02",194,261;
	end;
}

bat_a02,194,265,0	script	barri_warp_down#bat_a02a	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id1)
		warp "bat_a02",194,270;
	end;
}

bat_a02,177,130,0	script	barri_warp_up#bat_a02_b	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id2)
		warp "bat_a02",178,125;
	end;
}

bat_a02,177,128,0	script	barri_warp_down#bat_a02b	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id2)
		warp "bat_a02",178,134;
	end;
}

bat_a02,169,227,0	script	underladd#bat_a02_1	WARPNPC,1,1,{
OnTouch_:
	warp "bat_a02",178,228;
	end;
}

bat_a02,208,164,0	script	underladd#bat_a02_2	WARPNPC,1,1,{
OnTouch_:
	warp "bat_a02",200,171;
	end;
}

bat_a02,171,309,3	script	Acampamento Guillaume#flag11	1_FLAG_LION,{ end; }
bat_a02,149,310,3	script	Acampamento Guillaume#flag12	1_FLAG_LION,{ end; }
bat_a02,119,336,3	script	Acampamento Guillaume#flag13	1_FLAG_LION,{ end; }
bat_a02,118,357,3	script	Acampamento Guillaume#flag14	1_FLAG_LION,{ end; }
bat_a02,150,380,3	script	Acampamento Guillaume#flag15	1_FLAG_LION,{ end; }
bat_a02,173,380,3	script	Acampamento Guillaume#flag16	1_FLAG_LION,{ end; }
bat_a02,210,344,3	script	Acampamento Guillaume#flag17	1_FLAG_LION,{ end; }
bat_a02,350,325,3	script	Acampamento Guillaume#flag18	1_FLAG_LION,{ end; }
bat_a02,358,325,3	script	Acampamento Guillaume#flag19	1_FLAG_LION,{ end; }

bat_a02,138,12,3	script	Acampamento Croix#flag11	1_FLAG_EAGLE,{ end; }
bat_a02,108,36,3	script	Acampamento Croix#flag12	1_FLAG_EAGLE,{ end; }
bat_a02,108,63,3	script	Acampamento Croix#flag13	1_FLAG_EAGLE,{ end; }
bat_a02,136,87,3	script	Acampamento Croix#flag14	1_FLAG_EAGLE,{ end; }
bat_a02,167,86,3	script	Acampamento Croix#flag15	1_FLAG_EAGLE,{ end; }
bat_a02,199,49,3	script	Acampamento Croix#flag16	1_FLAG_EAGLE,{ end; }
bat_a02,168,16,3	script	Acampamento Croix#flag17	1_FLAG_EAGLE,{ end; }
bat_a02,357,74,3	script	Acampamento Croix#flag18	1_FLAG_EAGLE,{ end; }
bat_a02,348,74,3	script	Acampamento Croix#flag19	1_FLAG_EAGLE,{ end; }


bat_a02,1,5,3	script	countdown#bat_a02	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_a02", "Tenente de Guillaume Axl Rose : Vamos atacar e queirmar todo o Depósito de Suprimentos de Croix!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_a02", "Tenente de Croix Swandery : Mestre do Valhalla! Presenteia-nos com coragem e uma fé infalível!",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_a02", "Marollo VII : Guillaume Marollo, Croix Marollo! E todos os seus seguidores!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_a02", "Marollo VII : Ambos Exércitos são muito competitivos, de forma que nenhum acampamento será facilmente destruído. Isso significa que os Reinos de Morollo jamais serão derrotados!",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_a02", "Marollo VII : Acho que seria melhor encerrar a batalha, e chamarmos isso de um empate.",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_a02", "Marollo VII : Mantenham fé e lealdade por um momento, e vamos resolver a batalha pelo Desfiladeiro de Tierra.",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_a02", "Axl Rose, Swandery : Sim senhor.",bc_map,"0x99CC00";
	end;

OnTimer1830000:
	stopnpctimer;
	end;
}

bat_a02,1,8,3	script	time_event#bat_a02	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	set .Tierra_Timer, 0;
	stopnpctimer;
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;

OnTimer60000:
	set .Tierra_Timer, .Tierra_Timer+1;
	set .@event_timer, callfunc("bg_getarenainfo",2,"event_timer");
	if( .Tierra_Timer >= .@event_timer )
	{
		set $@TierraBG2, 2;
		if( $@TierraBG2_CroixScore > $@TierraBG2_GuillScore )
		{
			set $@TierraBG2_Victory, 2;
			mapannounce "bat_a02", "Marollo VII: O Exército de Croix destruíu mais depósitos de suprimentos, é o vencedor!",bc_map,"0x99CC00";
		}
		else if( $@TierraBG2_GuillScore > $@TierraBG2_CroixScore )
		{
			set $@TierraBG2_Victory, 1;
			mapannounce "bat_a02", "Marollo VII: O Exército de Guillaume destruíu mais depósitos de suprimentos, é o vencedor!",bc_map,"0x99CC00";
		}
		else {
			set $@TierraBG2_Victory, 3;
			mapannounce "bat_a02", "Marollo VII: Ambos Exércitos não cumpriram com seus objetivos, isso é uma empate!",bc_map,"0x99CC00";
		}
		
		donpcevent "Terapeuta da Batalha#a02_a::OnStop";
		donpcevent "Terapeuta da Batalha#a02_b::OnStop";
		donpcevent "countdown#bat_a02::OnStop";
		donpcevent "start#bat_a02_Out::OnBegin";
		enablenpc "Tenente de Guillaume#a02_a";
		enablenpc "Tenente de Croix#a02_b";
		bg_warp $@TierraBG2_id1,"bat_a02",50,374;
		bg_warp $@TierraBG2_id2,"bat_a02",42,16;
		stopnpctimer;
	}
	else
	{
		stopnpctimer;
		initnpctimer;
	}
	end;
}

-	script	start#bat_a02_Out	FAKE_NPC,{
	end;

OnBegin:
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_a02", "Por favor fale com o Tenente para pegar sua recompensa.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_a02", "O Tenente irá conceder sua recompensa por 30 segundos.",bc_map,"0x00ff00";
	mapannounce "bat_a02", "Em 30 segundos, o Tenente irá embora.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_a02", "Se você não falar com o Tenente, você não receberá sua recompensa.",bc_map,"0x00ff00";
	mapannounce "bat_a02", "Por favor, tenha cuidado.",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_a02", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	// Computação de Ranking
	set .@w,2;
	while(.@w > 0 && $@TierraBG2_Victory < 3)
	{
		deletearray .@bg_members_cid;
		deletearray .@bg_members_aid;
		bg_get_team getd("$@TierraBG2_id"+.@w);
		for( set .@i,0; .@i < getarraysize(.@bg_members_cid); set .@i,.@i+1 )
		{
			if( $@TierraBG2_Victory == .@w )
				callfunc "bg_ranking", .@bg_members_cid[.@i], 2, 1, 1;
			else
				callfunc "bg_ranking", .@bg_members_cid[.@i], 2, 0, 1;
		}
		set .@w, .@w - 1;
	}
	
	stopnpctimer;
	set $@TierraBG2_GuillScore,0;
	set $@TierraBG2_CroixScore,0;
	set $@TierraBG2_Victory, 0;
	bg_kick_team $@TierraBG2_id1;
	bg_kick_team $@TierraBG2_id2;
	disablenpc "Tenente de Guillaume#a02_a";
	disablenpc "Tenente de Croix#a02_b";
	mapwarp "bat_a02","bat_room",154,150;
	maprespawnguildid "bat_a02",0,3; // Just in case someone else
	bg_updatescore "bat_a02",0,0;
	end;
}

/*
bat_a02,351,75,3	script	Croix Camp Soldier#bat_a02_guide	4_M_RASWORD,{
	mes "Loading...";
	close;
}

bat_a02,356,326,3	script	Guillaume Camp Soldier#bat_a02_guide	4_M_RASWORD,{
	mes "Loading...";
	close;
}
*/

bat_a02,1,1,3	script	Liberar todos#a02	4_DOG01,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Precisa da minha ajuda?";
		next;
		switch(select("Liberar todos.:Cancelar.")) {
		case 1:
			mes "Tchau.";
			close2;
			mapwarp "bat_a02","bat_room",154,150;
			end;
		case 2:
			mes "Cancelado.";
			close;
		}
	}
}

// Recompensas
// -------------------------------------------------------
bat_a02,53,377,3	script	Tenente de Guillaume#a02_a	4_M_KY_HEAD,{
	callfunc("bg_rewardnpc", 2, $@TierraBG2_id1, $@TierraBG2_id2, $@TierraBG2_Victory, 0);
	end;

OnInit:
	disablenpc "Tenente de Guillaume#a02_a";
	end;
}

bat_a02,45,19,3	script	Tenente de Croix#a02_b	4_M_CRU_HEAD,{
	callfunc("bg_rewardnpc", 2, $@TierraBG2_id1, $@TierraBG2_id2, $@TierraBG2_Victory, 1);
	end;

OnInit:
	disablenpc "Tenente de Croix#a02_b";
	end;
}