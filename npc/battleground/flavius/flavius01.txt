/*.-----------------------------------------------------------------------------.
  .          ____                          __                                   .
  .         /\  _`\                       /\ \__  __                            .
  .         \ \ \/\_\  _ __    __     __  \ \ ,_\/\_\  __  __     __            .
  .          \ \ \/_/_/\`'__\/'__`\ /'__`\ \ \ \/\/\ \/\ \/\ \  /'__`\          .
  .           \ \ \s\ \ \ \//\  __//\ \d\.\_\ \ \_\ \ \ \ \_/ |/\  __/          .
  .            \ \____/\ \_\\ \____\ \__/.\_\\ \__\\ \_\ \___/ \ \____\         .
  .             \/___/  \/_/ \/____/\/__/\/_/ \/__/ \/_/\/__/   \/____/         .
  .                                                                             .
  .                  2014~2016 © Creative Services and Developent               .
  .                            www.creativesd.com.br                            .
  .                          www.sbkmod.ragnagreen.com                          .
  .-----------------------------------------------------------------------------.
  . Script:                                                                     .
  .    1ª Arena de Flavius                                                      .
  .-----------------------------------------------------------------------------.
  . Autor: Romulo SM (sbk_) (HerculesWS)                           Versão: 6.1  .
  .                                                                             .
  . Compátivel:	eAthena e derivações.                    	                    .
  .-----------------------------------------------------------------------------.
  . Descrição:                                                                  .
  .    Batalha no qual os jogadores devem destruir o cristal do adversário.     .
  *------------------------------------------------------------------------------*/
  
// Engine das Batalhas Campais da Arena de Flavius
// -------------------------------------------------------
bat_b01,15,15,3	script	start#bat_b01	CLEAR_NPC,{
OnInit:
	mapwarp "bat_b01","bat_room",154,150;
	queue_create(3, "1ª Arena de Flavius", "start#bat_b01::OnReadyCheck");
	set $@FlaviusBG1_id1, bg_create_team("bat_b01", 10, 290, 87, 75, 0, "start#bat_b01::OnGuillaumeQuit", "", "start#bat_b01::OnGuillWO");
	set $@FlaviusBG1_id2, bg_create_team("bat_b01", 390, 10, 311, 224, 1, "start#bat_b01::OnCroixQuit", "", "start#bat_b01::OnCroixWO");
	set .global_announce, 0;
	set .BlockStatus, 0;
	donpcevent "start#bat_b01::OnReadyCheck";
	end;

OnReadyCheck:
	if( .BlockStatus )
	{
		sleep 5000;
		donpcevent "start#bat_b01::OnReadyCheck";
	}
		
	set .BlockStatus, 1;

	set .@min_player, callfunc("bg_getarenainfo", 3, "min_player");
	set .@max_player, callfunc("bg_getarenainfo", 3, "max_player");
	set .@a_name$, callfunc("bg_getarenainfo", 3, "arena_name");
	set .@min_player, (.@min_player*2);
	
	if( .@max_player > MAX_BG_MEMBERS )
	{
		bg_console "(BG_ID: 3) Maximo de jogadores em uma equipe ultrapassa o limite estabelecido, definindo o maximo de jogadores para " + MAX_BG_MEMBERS + ".";
		set .@max_player, MAX_BG_MEMBERS;
	}
	
	queue_get_players 3;
	set .@playlist_team1, bg_team_size($@FlaviusBG1_id1);
	set .@playlist_team2, bg_team_size($@FlaviusBG1_id2);
	if( !.@queue_count )
	{
		set .BlockStatus, 0;
		end;
	}
	else if( $@FlaviusBG1 && $@FlaviusBG1 < 2 && .@queue_count && (.@playlist_team1 < .@max_player || .@playlist_team2 < .@max_player) )
	{
		set .@guill, 0;
		set .@croix, 0;
		set .@count_g, 0;
		set .@count_c, 0;
		
		// ---------------------------------------------------
		// -             Igualando os Exércitos              -
		// ---------------------------------------------------
		if( .@playlist_team1 < .@playlist_team2 )
		{
			set .@guill, .@playlist_team2-.@playlist_team1;
			for( set .@i,0; .@i < .@guill; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_g, .@count_g + 1;
				message .@queue_player$[.@i], "Você ingressou no Exército de Guillaume em uma batalha em " + .@a_name$ + " que já está acontecendo, e será teleportado em 10 segundos.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 3; // Reset
			set .@playlist_team1, bg_team_size($@FlaviusBG1_id1);
		}
		else if( .@playlist_team2 < .@playlist_team1 )
		{
			set .@croix, .@playlist_team1-.@playlist_team2;
			for( set .@i,0; .@i < .@croix; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG2_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_c, .@count_c + 1;
				message .@queue_player$[.@i], "Você ingressou no Exército de Croix em uma batalha em " + .@a_name$ + " que já está acontecendo, e será teleportado em 10 segundos.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 3; // Reset
			set .@playlist_team2, bg_team_size($@FlaviusBG1_id2);
		}
		// ---------------------------------------------------
		// -            Expandindo os Exércitos              -
		// ---------------------------------------------------
		if( .@queue_count && .@playlist_team1 == .@playlist_team2 && .@playlist_team1 < .@max_player && .@playlist_team2 < .@max_player )
		{
			set .@guill, .@max_player-.@playlist_team1;
			set .@croix, .@max_player-.@playlist_team2;
			
			if( .@queue_count > (.@guill+.@croix) )
				set .@queue_count, (.@guill+.@croix);
				
			if( .@queue_count%2 )
				set .@queue_count, .@queue_count-1;
			
			set .@size, .@queue_count/2;
			for( set .@i, 0; .@i < .@size; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_g$[.@count_g], .@queue_player$[.@i];
				set .@count_g, .@count_g + 1;
			}
			
			for( set .@i,.@size; .@i < .@queue_count; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG1_id2, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_c$[.@count_c], .@queue_player$[.@i];
				set .@count_c, .@count_c + 1;
			}
		}
		
		if( .@count_c || .@count_g )
		{
			announce "[Batalhas Campais]: " + (.@count_c+.@count_g) + " jogadores estão sendo enviados para uma batalha que está acontecendo em " + .@a_name$ + ", os Exércitos foram equilibrados e expandidos.", bc_all, "0xFFCE00";
			sleep 3000;
		}
		
		deletearray .@queue_cid;
		deletearray .@queue_aid;
		deletearray .@queue_player$;
		set .@queue_count, 0;
		queue_get_players 3; // Reset
		
		set .@queue_size, queue_size(3);
		set .@team1_size, bg_team_size($@FlaviusBG1_id1);
		set .@team2_size, bg_team_size($@FlaviusBG1_id2);
		
		if( .@team1_size < .@max_player || .@team2_size < .@max_player )
		{
			set .@size, (2*.@max_player)-(.@team1_size+.@team2_size)-.@queue_size;
			announce "[Batalhas Campais]: Ainda há " + (.@size) + " vagas para uma batalha que está acontecendo em " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	else if( $@FlaviusBG1 && !.global_announce && .@queue_count >= .@min_player )
	{
		announce "[Batalhas Campais]: A batalha está completa, mas há uma batalha acontecendo em " + .@a_name$ + ", todos jogadores que estão na fila de espera devem aguardar.", bc_all, "0xFFCE00";
		set .global_announce, 1;
	}
	else if( .@queue_count < .@min_player )
		announce "[Batalhas Campais]: Falta " + (.@min_player-.@queue_count) + " jogadores na batalha em " + .@a_name$ + "!", bc_all, "0xFFCE00";
	else
	{
		// ---------------------------------------------------
		// -                  Balanceamento                  -
		// ---------------------------------------------------
		if( .@queue_count > (.@max_player*2) )
			set .@queue_count, (.@max_player*2);
			
		if( .@queue_count%2 )
			set .@queue_count, .@queue_count-1;
			
		if( .@queue_count >= .@min_player )
		{
			for( set .@i,0; .@i < .@queue_count; set .@i, .@i + 2 )
			{
				// Ingressando no Exército de Guillaume
				bg_join_team $@FlaviusBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				message .@queue_player$[.@i], "Você ingressou no Exército de Guillaume, será teleportado em 10 segundos.";
				
				// Ingressando no Exército de Croix
				bg_join_team $@FlaviusBG1_id2, 1, .@queue_aid[.@i+1];
				queue_leave 0, .@queue_aid[.@i+1];
				message .@queue_player$[.@i+1], "Você ingressou no Exército de Croix, será teleportado em 10 segundos.";
			}
		}
		
		bg_refresh_patent($@FlaviusBG1_id1);
		bg_refresh_patent($@FlaviusBG1_id2);
		set .global_announce, 0;
		set $@FlaviusBG1, 1;
		announce "[Batalhas Campais]: Ambos os Exércitos, estão completo boa sorte nas batalhas em " + .@a_name$ + ".", bc_all, "0xFFCE00";
		set $@FlaviusBG1_CroixScore, 0;
		set $@FlaviusBG1_GuillScore, 0;
		bg_updatescore "bat_b01",0,0;
		donpcevent "start#bat_b01::OnEnable";
		sleep 3000;
		initnpctimer;
		
		if( $@BG_MODE && callfunc("bg_checkchangearena") == true )
			donpcevent "BattleGround#queue_main::OnReload";
			
		if( .@queue_count < (2*.@max_player) )
		{
			sleep 3000;
			set .@count, (2*.@max_player)-.@queue_count;
			announce "[Batalhas Campais]: Ainda há " + .@queue_count + " vagas nas batalhas em " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	set .BlockStatus, 0;
	end;

OnEnable:
	donpcevent "OBJ#bat_b01_a::OnKill";
	donpcevent "OBJ#bat_b01_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::OnKill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardian#bat_b01_a::OnKill";
	donpcevent "guardian#bat_b01_b::OnKill";
	donpcevent "guardian#bat_b01_a::OnEnable";
	donpcevent "guardian#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b01_a::OnRed";
	donpcevent "cell#bat_b01_b::OnRed";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Tenente de Guillaume#b01_a";
	disablenpc "Tenente de Croix#b01_b";
	donpcevent "countdown#bat_b01::OnEnable";
	donpcevent "time_event#bat_b01::OnEnable";
	initnpctimer;
	end;

OnReset:
	donpcevent "OBJ#bat_b01_a::OnKill";
	donpcevent "OBJ#bat_b01_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::OnKill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardian#bat_b01_a::OnKill";
	donpcevent "guardian#bat_b01_b::OnKill";
	donpcevent "guardian#bat_b01_a::OnEnable";
	donpcevent "guardian#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b01_a::OnRed";
	donpcevent "cell#bat_b01_b::OnRed";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Tenente de Guillaume#b01_a";
	disablenpc "Tenente de Croix#b01_b";
	bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
	bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
	end;

OnGuillaumeQuit:
OnCroixQuit:
	set .@char_id, getcharid(0);
	callfunc "bg_ranking", .@char_id, 3, 0, 1; // BG Queue Ranking: Computer negative vote.
	donpcevent "start#bat_b01::OnReadyCheck";
	end;
	
OnGuillWO:
	if( queue_size(3) )
		end;
		
	set $@FlaviusBG1, 2;
	set $@FlaviusBG1_Victory, 2;
	donpcevent "Terapeuta da Batalha#b01_a::OnStop";
	donpcevent "Terapeuta da Batalha#b01_b::OnStop";
	mapannounce "bat_b01", "Tenente de Croix Swandery: Os covardes de Guillaume fugiram. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
	enablenpc "Tenente de Guillaume#b01_a";
	enablenpc "Tenente de Croix#b01_b";
	donpcevent "countdown#bat_b01::OnStop";
	donpcevent "time_event#bat_b01::OnStop";
	donpcevent "start#bat_b01_Out::OnBegin";
	sleep 2000;
	bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
	bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
	end;
	
OnCroixWO:
	if( queue_size(3) )
		end;
		
	set $@FlaviusBG1, 2;
	set $@FlaviusBG1_Victory, 1;
	donpcevent "Terapeuta da Batalha#b01_a::OnStop";
	donpcevent "Terapeuta da Batalha#b01_b::OnStop";
	mapannounce "bat_b01", "Tenente de Guillaume Axl Rose: Os covardes de Croix fugiram. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
	enablenpc "Tenente de Guillaume#b01_a";
	enablenpc "Tenente de Croix#b01_b";
	donpcevent "countdown#bat_b01::OnStop";
	donpcevent "time_event#bat_b01::OnStop";
	donpcevent "start#bat_b01_Out::OnBegin";
	sleep 2000;
	bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
	bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
	end;

// Checagem de Jogadores na Arena.
// -------------------------------------------------------
OnTimer10000:
	stopnpctimer;
	if( !getmapusers("bat_b01") )
	{
		set $@FlaviusBG1, 0;
		bg_kick_team $@FlaviusBG1_id1;
		bg_kick_team $@FlaviusBG1_id2;
		donpcevent "start#bat_b01_Out::OnStop";
		
		if( !$@BG_MODE || $BG_ARENA == 3 )
			donpcevent "start#bat_b01::OnReadyCheck";
	}
	else
		initnpctimer;
	end;
}

bat_b01,1,1,3	script	OBJ#bat_b01_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id1,"bat_b01",61,150,"Cristal Vermelho",1915,"","OBJ#bat_b01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_a::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "O Cristal de Guillaume foi destruído.",bc_map,"0xFFCE00";
		set $@FlaviusBG1_CroixScore, $@FlaviusBG1_CroixScore+1;
		set .@score_round, callfunc("bg_getarenainfo",3,"score_round");
		
		if( $@FlaviusBG1_CroixScore >= .@score_round )
		{
			set $@FlaviusBG1, 2;
			set $@FlaviusBG1_Victory, 2;
			enablenpc "Tenente de Guillaume#b01_a";
			enablenpc "Tenente de Croix#b01_b";
			donpcevent "time#bat_b01::OnStop";
			donpcevent "countdown#bat_b01::OnStop";
			donpcevent "time_event#bat_b01::OnStop";
			donpcevent "start#bat_b01_Out::OnBegin";
			bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
			bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		}
		else {
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
			bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
			bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
		}
		bg_updatescore "bat_b01",$@FlaviusBG1_GuillScore,$@FlaviusBG1_CroixScore;
	}
	end;
}

bat_b01,1,2,3	script	OBJ#bat_b01_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id2,"bat_b01",328,150,"Cristal Azul",1914,"","OBJ#bat_b01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_b::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "O Cristal de Croix foi destruído.",bc_map,"0xFFCE00";
		set $@FlaviusBG1_GuillScore, $@FlaviusBG1_GuillScore+1;
		set .@score_round, callfunc("bg_getarenainfo",3,"score_round");
		if( $@FlaviusBG1_GuillScore >= .@score_round )
		{
			set $@FlaviusBG1, 2;
			set $@FlaviusBG1_Victory, 1;
			enablenpc "Tenente de Guillaume#b01_a";
			enablenpc "Tenente de Croix#b01_b";
			donpcevent "time#bat_b01::OnStop";
			donpcevent "countdown#bat_b01::OnStop";
			donpcevent "time_event#bat_b01::OnStop";
			donpcevent "start#bat_b01_Out::OnBegin";
			bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
			bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		}
		else {
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
			bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
			bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
		}
		bg_updatescore "bat_b01",$@FlaviusBG1_GuillScore,$@FlaviusBG1_CroixScore;
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id1,"bat_b01",108,159,"Guardião de Guillaume",1949,"Guardião","guardian#bat_b01_a::OnMyMobDead";
	bg_monster $@FlaviusBG1_id1,"bat_b01",108,141,"Guardião de Guillaume",1949,"Guardião","guardian#bat_b01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","guardian#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_a::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_a::OnGreen";
		mapannounce "bat_b01", "O Guardião que protegia o Cristal de Guillaume foi destruído.",bc_map,"0xFFCE00";
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id2,"bat_b01",307,160,"Guardião de Croix",1949,"Guardião","guardian#bat_b01_b::OnMyMobDead";
	bg_monster $@FlaviusBG1_id2,"bat_b01",307,138,"Guardião de Croix",1949,"Guardião","guardian#bat_b01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","guardian#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_b::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_b::OnGreen";
		mapannounce "bat_b01", "O Guardião que protegia o Cristal de Croix foi destruído.",bc_map,"0xFFCE00";
	}
	end;
}

bat_b01,1,4,3	script	cell#bat_b01_a	CLEAR_NPC,{
OnRed:
	setcell "bat_b01",62,149,60,151,cell_basilica,1;
	setcell "bat_b01",62,149,60,151,cell_walkable,0;
	end;

OnGreen:
	setcell "bat_b01",62,149,60,151,cell_basilica,0;
	setcell "bat_b01",62,149,60,151,cell_walkable,1;
	end;
}

bat_b01,1,5,3	script	cell#bat_b01_b	CLEAR_NPC,{
OnRed:
	setcell "bat_b01",327,151,329,149,cell_basilica,1;
	setcell "bat_b01",327,151,329,149,cell_walkable,0;
	end;

OnGreen:
	setcell "bat_b01",327,151,329,149,cell_basilica,0;
	setcell "bat_b01",327,151,329,149,cell_walkable,1;
	end;
}

bat_b01,1,6,1	script	time#bat_b01	CLEAR_NPC,{
OnEnable:
	donpcevent "Terapeuta da Batalha#b01_a::OnEnable";
	donpcevent "Terapeuta da Batalha#b01_b::OnEnable";
	end;

OnStop:
	donpcevent "Terapeuta da Batalha#b01_a::OnStop";
	donpcevent "Terapeuta da Batalha#b01_b::OnStop";
	end;
}

bat_b01,10,294,3	script	Terapeuta da Batalha#b01_a	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta da Batalha]";
	mes "Apenas feche seus olhos";
	mes "e respire fundo.";
	mes "Você pode se libertar da dor.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;
	
OnTimer26500:
	stopnpctimer;
	donpcevent "Terapeuta da Batalha#b01_a::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Terapeuta da Batalha#b01_a";
	end;

OnStop:
	disablenpc "Terapeuta da Batalha#b01_a";
	stopnpctimer;
	end;
}

bat_b01,389,14,3	script	Terapeuta da Batalha#b01_b	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta da Batalha]";
	mes "Apenas feche seus olhos";
	mes "e respire fundo.";
	mes "Você pode se libertar da dor.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Terapeuta da Batalha#b01_b::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Terapeuta da Batalha#b01_b";
	end;

OnStop:
	disablenpc "Terapeuta da Batalha#b01_b";
	stopnpctimer;
	end;
}

bat_b01,1,5,3	script	countdown#bat_b01	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_b01", "Tenente de Guillaume Axl Rose : Vamos atacar e destruir o Cristal!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_b01", "Tenente de Croix Swandery : Mesmo que Guillaume esteja lutando contra nossa vitória, ela já é nossa! Vamos mostrar a eles o nosso poder.",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_b01", "Marollo VII : Guillaume Marollo, Croix Marollo! E seus seguidores!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_b01", "Marollo VII : Ambos Exércitos são muito competitivos, por isso é difícil julgar qual grupamento é superior.",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_b01", "Marollo VII : Essa batalha Flavius é um desperdício de tempo. Logo decidirei a vitória e a derrota pelo seu progresso.",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_b01", "Marollo VII : Se vocês não aceitarem esse resultado, tentem a sorte em outro vale de batalha!",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_b01", "Axl Rose, Swandery : Sim, senhor.",bc_map,"0x99CC00";
	stopnpctimer;
	end;
}

bat_b01,1,8,3	script	time_event#bat_b01	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	set .Flavius_Timer, 0;
	stopnpctimer;
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;
	
OnTimer60000:
	set .Flavius_Timer, .Flavius_Timer+1;
	set .@event_timer, callfunc("bg_getarenainfo", 3, "event_timer");
	if( .Flavius_Timer >= .@event_timer )
	{
		set $@FlaviusBG1, 2;
		if( $@FlaviusBG1_GuillScore < $@FlaviusBG1_CroixScore )
		{
			mapannounce "bat_b01", "Marollo VII: O Exército de Croix é o vitorioso por seu desempenho nas Batalhas!",bc_map,"0x99CC00";
			set $@FlaviusBG1_Victory, 2;
		}
		else if( $@FlaviusBG1_GuillScore > $@FlaviusBG1_CroixScore )
		{
			mapannounce "bat_b01", "Marollo VII: O Exército de Guillaume é o vitorioso por seu desempenho nas Batalhas!",bc_map,"0x99CC00";
			set $@FlaviusBG1_Victory, 1;
		}
		else
		{
			mapannounce "bat_b01", "Marollo VII: Ambos Exércitos não cumpriram o objetivo, isto é sinal de um empate!",bc_map,"0x99CC00";
			set $@FlaviusBG1_Victory, 3;
		}
		
		stopnpctimer;
		donpcevent "time#bat_b01::OnStop";
		donpcevent "countdown#bat_b01::OnStop";
		donpcevent "start#bat_b01_Out::OnBegin";
		bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
		bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		enablenpc "Tenente de Guillaume#b01_a";
		enablenpc "Tenente de Croix#b01_b";
	}
	else
	{
		stopnpctimer;
		initnpctimer;
	}
	end;
}

-	script	start#bat_b01_Out	FAKE_NPC,{
	end;

OnBegin:
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_b01", "Por favor fale com o Tenente para pegar sua recompensa.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_b01", "O Tenente irá conceder sua recompensa por 30 segundos.",bc_map,"0x00ff00";
	mapannounce "bat_b01", "Em 30 segundos, o Tenente irá embora.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_b01", "Se você não falar com o Tenente, você não receberá sua recompensa.",bc_map,"0x00ff00";
	mapannounce "bat_b01", "Por favor, tenha cuidado.",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_b01", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	// Computação de Ranking
	set .@w,2;
	while(.@w > 0 && $@FlaviusBG1_Victory < 3)
	{
		deletearray .@bg_members_cid;
		deletearray .@bg_members_aid;
		bg_get_team getd("$@FlaviusBG1_id"+.@w);
		for( set .@i,0; .@i < getarraysize(.@bg_members_cid); set .@i,.@i+1 )
		{
			if( $@FlaviusBG1_Victory == .@w )
				callfunc "bg_ranking", .@bg_members_cid[.@i], 3, 1, 1;
			else
				callfunc "bg_ranking", .@bg_members_cid[.@i], 3, 0, 1;
		}
		set .@w, .@w - 1;
	}
	
	stopnpctimer;
	set $@FlaviusBG1_GuillScore,0;
	set $@FlaviusBG1_CroixScore,0;
	set $@FlaviusBG1_Victory, 0;
	bg_kick_team $@FlaviusBG1_id1;
	bg_kick_team $@FlaviusBG1_id2;
	disablenpc "Tenente de Guillaume#b01_a";
	disablenpc "Tenente de Croix#b01_b";
	mapwarp "bat_b01","bat_room",154,150;
	maprespawnguildid "bat_b01",0,3; // Just in case someone else
	end;
}

bat_b01,81,83,3	script	Acampamento Guillaume#flag21	1_FLAG_LION,{ end; }
bat_b01,94,83,3	script	Acampamento Guillaume#flag22	1_FLAG_LION,{ end; }
bat_b01,81,66,3	script	Acampamento Guillaume#flag23	1_FLAG_LION,{ end; }
bat_b01,94,66,3	script	Acampamento Guillaume#flag24	1_FLAG_LION,{ end; }
bat_b01,139,142,3	script	Acampamento Guillaume#flag25	1_FLAG_LION,{ end; }
bat_b01,139,158,3	script	Acampamento Guillaume#flag26	1_FLAG_LION,{ end; }
bat_b01,110,161,3	script	Acampamento Guillaume#flag27	1_FLAG_LION,{ end; }
bat_b01,110,137,3	script	Acampamento Guillaume#flag28	1_FLAG_LION,{ end; }
bat_b01,63,135,3	script	Acampamento Guillaume#flag29	1_FLAG_LION,{ end; }
bat_b01,63,165,3	script	Acampamento Guillaume#flag30	1_FLAG_LION,{ end; }
bat_b01,10,296,3	script	Acampamento Guillaume#flag31	1_FLAG_LION,{ end; }

bat_b01,306,233,3	script	Acampamento Croix#flag21	1_FLAG_EAGLE,{ end; }
bat_b01,317,233,3	script	Acampamento Croix#flag22	1_FLAG_EAGLE,{ end; }
bat_b01,306,216,3	script	Acampamento Croix#flag23	1_FLAG_EAGLE,{ end; }
bat_b01,317,216,3	script	Acampamento Croix#flag24	1_FLAG_EAGLE,{ end; }
bat_b01,257,158,3	script	Acampamento Croix#flag25	1_FLAG_EAGLE,{ end; }
bat_b01,257,141,3	script	Acampamento Croix#flag26	1_FLAG_EAGLE,{ end; }
bat_b01,297,164,3	script	Acampamento Croix#flag27	1_FLAG_EAGLE,{ end; }
bat_b01,297,136,3	script	Acampamento Croix#flag28	1_FLAG_EAGLE,{ end; }
bat_b01,336,161,3	script	Acampamento Croix#flag29	1_FLAG_EAGLE,{ end; }
bat_b01,336,139,3	script	Acampamento Croix#flag30	1_FLAG_EAGLE,{ end; }
bat_b01,389,16,3	script	Acampamento Croix#flag31	1_FLAG_EAGLE,{ end; }

bat_b01,1,10,3	script	Liberar Todos#b01	4_DOG01,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Precisa de minha ajuda?";
		next;
		switch(select("Liberar todos.:Cancelar.")) {
		case 1:
			mes "Tchau.";
			close2;
			mapwarp "bat_b01","bat_room",154,150;
			end;
		case 2:
			mes "Cancelado.";
			close;
		}
	}
}

// Recompensas
// -------------------------------------------------------
bat_b01,10,294,3	script	Tenente de Guillaume#b01_a	4_M_KY_HEAD,{
	callfunc("bg_rewardnpc", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, 0);
	end;

OnInit:
	disablenpc "Tenente de Guillaume#b01_a";
	end;
}

bat_b01,389,14,3	script	Tenente de Croix#b01_b	4_M_CRU_HEAD,{
	callfunc("bg_rewardnpc", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, 1);
	end;

OnInit:
	disablenpc "Tenente de Croix#b01_b";
	end;
}